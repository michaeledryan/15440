apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.7
version = '1.0'

def components = [
    [name: "master"],
    [name: "worker"],
    [name: "client"]
]

components.each { comp ->
    project.task(comp.name, type: Jar, dependsOn: 'classes') {
        manifest {
            attributes 'Implementation-Title': 'Migratable Processes', 'Implementation-Version': version, 'Main-Class': comp.name+'.Main'
        }
        from {
            [ configurations.compile.collect { it.isDirectory() ? it : zipTree(it) },
              fileTree('build/classes/main/') ]
        }
        archiveName=comp.name+".jar"
        destinationDir=file(".")
    }
}

repositories {
    mavenCentral()
}

task all (dependsOn: [ 'assemble', 'master', 'worker', 'client' ]) {

}

task cleanup (type: Delete, dependsOn: clean) {
    delete 'master.jar', 'worker.jar', 'client.jar'
}

dependencies {
    compile group: 'junit', name: 'junit', version: '4.+'
    compile group: 'commons-io', name: 'commons-io', version: '2.0.1'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.2'
}

test {
    testLogging {
        // Show that tests are run in the command-line output
        events 'started', 'passed'
    }
}

